plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.kyrptonaught.dev' }
    maven { url 'https://dl.cloudsmith.io/public/origamiking3612/origamikings-api/maven/' }
    maven {
        name 'TerraformersMC'
        url 'https://maven.terraformersmc.com/'
    }
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=orm"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += ['src/main/generated']
        }
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Uncomment the following line to enable the deprecated Fabric API modules.
    // These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.
    // modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"

    modImplementation "net.origamiking:Origamikings-API:${origamikings_api}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.20:${geckolib_version}"

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    modImplementation "net.kyrptonaught:customportalapi:${portal_api_version}"
    include "net.kyrptonaught:customportalapi:${portal_api_version}"

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "cloudsmith"
            url = "https://maven.cloudsmith.io/origamiking3612/origamikings-robotics-mod/"
            credentials {
                username = System.getenv("CLOUDSMITH_USERNAME")
                password = System.getenv("CLOUDSMITH_PASSWORD")
            }
        }
    }
}
modrinth {
//		debugMode = true
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project_id}"
    versionNumber = "${mod_version}"
    versionType = "${version_type}"
    uploadFile = remapJar
    gameVersions = ['1.20.1'] // Must be an array, even with only one version
    dependencies {
        // scope.type
        // The scope can be `required`, `optional`, `incompatible`, or `embedded`
        // The type can either be `project` or `version`
        embedded.project "geckolib"
        embedded.project "fabric-api"
        embedded.project "origamikings-api"

    }
    changelog = "1.2.0-1.20.1: The \"Cybertron Update\" Version:\n" + "\n" +
            "Added Transformium Alloy and Transformium Alloy Bricks, added Cybertron Dimension, 2 new characters, and added Advancements!" + "\n" + "\n" +
            "Please report any bugs [here](https://github.com/OrigamiKing3612/OrigamiKings-Robotics-Mod/issues)"
}